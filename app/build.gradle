apply plugin: 'com.android.application'
android {
    packagingOptions {
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/INDEX.LIST'
        exclude 'META-INF/io.netty.versions.properties'
        exclude 'META-INF/rxjava.properties'
        exclude 'lib/x86_64/darwin/libscrypt.dylib'
        exclude 'jackson-core-asl-1.8.6.jar:META-INF/ASL2.0'
        exclude 'jackson-core-asl-1.8.6.jar:META-INF/LICENSE'
        exclude 'jackson-core-asl-1.8.6.jar:META-INF/NOTICE'
        exclude 'org/bitcoinj/crypto/mnemonic/wordlist/english.txt'
        exclude 'org/bitcoinj/crypto/cacerts'
        exclude 'org.bitcoin.production.checkpoints.txt'
        exclude 'org.bitcoin.test.checkpoints.txt'
        exclude 'org/codehaus/jackson/impl/VERSION.txt'
        exclude 'org/codehaus/jackson/map/VERSION.txt'
        exclude 'org/fusesource/leveldbjni/version.txt'
        exclude 'org/h2/util/data.zip'
        exclude 'META-INF/native/linux32/libleveldbjni.so'
        exclude 'META-INF/native/linux64/libleveldbjni.so'
        exclude 'META-INF/native/osx/libleveldbjni.jnilib'
        exclude 'META-INF/native/windows32/leveldbjni.dll'
        exclude 'META-INF/native/windows64/leveldbjni.dll'
        exclude 'META-INF/services/java.sql.Driver'
        exclude 'com/google/thirdparty/publicsuffix/PublicSuffixPatterns.gwt.xml'
        exclude 'com/google/thirdparty/publicsuffix/PublicSuffixType.gwt.xml'
        exclude 'com/mysql/jdbc/Charsets.properties'
        exclude 'com/mysql/jdbc/LocalizedErrorMessages.properties'
        exclude 'com/mysql/jdbc/TimeZoneMapping.properties'
        exclude 'com/mysql/jdbc/configs/3-0-Compat.properties'
        exclude 'com/mysql/jdbc/configs/5-0-Compat.properties'
        exclude 'com/mysql/jdbc/configs/clusterBase.properties'
        exclude 'com/mysql/jdbc/configs/coldFusion.properties'
        exclude 'com/mysql/jdbc/configs/fullDebug.properties'
        exclude 'com/mysql/jdbc/configs/maxPerformance.properties'
        exclude 'com/mysql/jdbc/configs/solarisMaxPerformance.properties'
    }
    compileSdkVersion 25
    buildToolsVersion '25.0.2'

    defaultConfig {
        applicationId 'it.inbitcoin.altana'
        minSdkVersion 14
        targetSdkVersion 25
        versionCode 39
        // Don't use defaultConfig.getProperty("versionCode") in versionName
        // because it breaks F-Droid!
        versionName '17.04.26'
    }
    buildTypes {
        release {
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard-rules.pro'
            multiDexEnabled true
            minifyEnabled true
            shrinkResources true
        }
        debug {
            multiDexEnabled true
        }
    }
    splits {
        abi {
            enable true
            universalApk true
        }
    }
    project.ext.versionCodes = ['armeabi': 1, 'armeabi-v7a': 2, 'arm64-v8a': 3, 'mips': 5, 'mips64': 6, 'x86': 8, 'x86_64': 9]
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.versionCodeOverride =
                    variant.mergedFlavor.minSdkVersion.apiLevel * 1000000 + project.ext.versionCodes.get(output.getFilter(
                            com.android.build.OutputFile.ABI), 0) * 10000 + android.defaultConfig.versionCode
        }
    }
    productFlavors {
        production {
        }
        production_21 {
            minSdkVersion 21
        }
        btctestnet {
            applicationId 'it.inbitcoin.altana.test.testnet'
            versionName String.format("%s_testnet", defaultConfig.getProperty("versionName"))
        }
        btctestnet_21 {
            minSdkVersion 21
            applicationId 'it.inbitcoin.altana.test.testnet'
            versionName String.format("%s_testnet", defaultConfig.getProperty("versionName"))
        }
        publicregtest {
            applicationId "it.inbitcoin.altana.publicregtest"
            versionName String.format("%s_publicregtest", defaultConfig.getProperty("versionName"))
        }
        publicregtest_21 {
            minSdkVersion 21
            applicationId "it.inbitcoin.altana.publicregtest"
            versionName String.format("%s_publicregtest", defaultConfig.getProperty("versionName"))
        }
        regtest {
            applicationId 'it.inbitcoin.altana.regtest'
            versionName String.format("%s_regtest", defaultConfig.getProperty("versionName"))
        }
        regtest_21 {
            minSdkVersion 21
            applicationId 'it.inbitcoin.altana.regtest'
            versionName String.format("%s_regtest", defaultConfig.getProperty("versionName"))
        }
        alpha {
            applicationId 'it.inbitcoin.altana.alpha'
            versionName String.format("%s_alpha", defaultConfig.getProperty("versionName"))
        }
        alpha_21 {
            minSdkVersion 21
            applicationId 'it.inbitcoin.altana.alpha'
            versionName String.format("%s_alpha", defaultConfig.getProperty("versionName"))
        }
    }

    sourceSets {
        production_21 {
            java { srcDir 'src/production/java' }
            assets { srcDir 'src/production/assets' }
        }
        btctestnet_21 {
            java { srcDir 'src/btctestnet/java' }
            assets { srcDir 'src/btctestnet/assets' }
            res { srcDir 'src/btctestnet/res' }
        }
        publicregtest_21 {
            java { srcDir 'src/publicregtest/java' }
            assets { srcDir 'src/publicregtest/assets' }
            res { srcDir 'src/publicregtest/res' }
        }
        regtest_21 {
            java { srcDir 'src/regtest/java' }
            assets { srcDir 'src/regtest/assets' }
            res { srcDir 'src/regtest/res' }
        }
        alpha_21 {
            java { srcDir 'src/alpha/java' }
            assets { srcDir 'src/alpha/assets' }
            res { srcDir 'src/alpha/res' }
        }
     }

    dexOptions {
        maxProcessCount 2
        javaMaxHeapSize "2048M"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    lintOptions {
        disable 'MissingTranslation'
        abortOnError false
    }
}

configurations {
    compile.exclude group: "org.json", module: "json"
}

dependencies {
    compile project(':bitcoinj:core'), project(':jawampa'),
            project(':jawampa:jawampa-core'), project(':jawampa:jawampa-netty'),
            files('libwally-core/src/swig_java/wallycore.jar')
    compile 'com.fasterxml.jackson:jackson-datatype-json-org:1.8.0'
    compile 'com.github.dmytrodanylyk.circular-progress-button:library:1.1.3'
    compile 'com.android.support:support-v4:25.2.0'
    compile 'com.android.support:appcompat-v7:25.2.0'
    compile 'com.android.support:design:25.2.0'
    compile 'com.android.support:recyclerview-v7:25.2.0'
    compile 'com.google.zxing:core:3.2.1'
    compile 'com.google.protobuf:protobuf-java:2.6.1'
    compile 'com.android.support:multidex:1.0.1'
    compile 'com.squareup.okhttp:okhttp:2.7.5'
    compile 'com.github.medyo:android-about-page:1.2'
    compile 'com.afollestad.material-dialogs:core:0.9.3.0'
    compile 'com.maksim88:PasswordEditText:v0.9'
    compile 'com.androidadvance:topsnackbar:1.1.1'
    // used to redirect bitcoinj logging to android in debug builds
    debugCompile 'com.noveogroup.android:android-logger:1.3.5'
    compile 'com.android.support:gridlayout-v7:25.2.0'
}

repositories {
    jcenter()
}

apply plugin: 'findbugs'

task findbugs(type: FindBugs) {
    ignoreFailures = true
    effort = "default"
    reportLevel = "low"
    excludeFilter = new File("${project.rootDir}/findbugs/findbugs-filter.xml")
    classes = files("${project.rootDir}/app/build/intermediates/classes/production/release/com/greenaddress/")
    source = fileTree('src/main/java/com/greenaddress/')
    classpath = files()
    reports {
        xml.enabled = true
        html.enabled = false
        xml {
            destination "$project.buildDir/findbugs/findbugs-output.xml"
        }
        html {
            destination "$project.buildDir/findbugs/findbugs-output.html"
        }
    }
}
