apply plugin: 'com.android.application'
android {
    packagingOptions {
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/INDEX.LIST'
        exclude 'META-INF/io.netty.versions.properties'
        exclude 'META-INF/rxjava.properties'
        exclude 'lib/x86_64/darwin/libscrypt.dylib'
        exclude 'org/bitcoinj/crypto/mnemonic/wordlist/english.txt'
        exclude 'org/bitcoinj/crypto/cacerts'
        exclude 'org.bitcoin.production.checkpoints.txt'
        exclude 'org.bitcoin.test.checkpoints.txt'
        exclude 'org/codehaus/jackson/impl/VERSION.txt'
        exclude 'org/codehaus/jackson/map/VERSION.txt'
        exclude 'org/fusesource/leveldbjni/version.txt'
        exclude 'org/h2/util/data.zip'
        exclude 'META-INF/native/linux32/libleveldbjni.so'
        exclude 'META-INF/native/linux64/libleveldbjni.so'
        exclude 'META-INF/native/osx/libleveldbjni.jnilib'
        exclude 'META-INF/native/windows32/leveldbjni.dll'
        exclude 'META-INF/native/windows64/leveldbjni.dll'
        exclude 'META-INF/services/java.sql.Driver'
        exclude 'com/google/thirdparty/publicsuffix/PublicSuffixPatterns.gwt.xml'
        exclude 'com/google/thirdparty/publicsuffix/PublicSuffixType.gwt.xml'
        exclude 'com/mysql/jdbc/Charsets.properties'
        exclude 'com/mysql/jdbc/LocalizedErrorMessages.properties'
        exclude 'com/mysql/jdbc/TimeZoneMapping.properties'
        exclude 'com/mysql/jdbc/configs/3-0-Compat.properties'
        exclude 'com/mysql/jdbc/configs/5-0-Compat.properties'
        exclude 'com/mysql/jdbc/configs/clusterBase.properties'
        exclude 'com/mysql/jdbc/configs/coldFusion.properties'
        exclude 'com/mysql/jdbc/configs/fullDebug.properties'
        exclude 'com/mysql/jdbc/configs/maxPerformance.properties'
        exclude 'com/mysql/jdbc/configs/solarisMaxPerformance.properties'
    }
    compileSdkVersion 27
    buildToolsVersion '28.0.3'

    defaultConfig {
        applicationId 'it.inbitcoin.altana'
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 90
        // Don't use defaultConfig.getProperty("versionCode") in versionName
        // because it breaks F-Droid!
        versionName '18.12'
    }
    flavorDimensions 'normal'
    buildTypes {
        release {
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard-rules.pro'
            multiDexEnabled true
            minifyEnabled true
            shrinkResources true
            matchingFallbacks = ['normal']
        }
        debug {
            multiDexEnabled true
            matchingFallbacks = ['normal']
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }
    }
    splits {
        abi {
            enable false
            reset()
            include 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
            universalApk true
        }
    }
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.versionCodeOverride = 22000000 + android.defaultConfig.versionCode
        }
    }
    productFlavors {
        production {
        }
    }

    sourceSets {
        production {
            res { srcDir 'altana-android-res/production/res' }
        }
     }

    dexOptions {
        maxProcessCount 2
        javaMaxHeapSize "2048M"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        abortOnError true
        ignoreWarnings false
        disable 'MissingTranslation'
        abortOnError false
    }
}

configurations {
    api.exclude group: "org.json", module: "json"
}

dependencies {
    api project(':bitcoinj:core'), project(':jawampa'),
            project(':jawampa:jawampa-core'), project(':jawampa:jawampa-netty'),
            files('libwally-core/src/swig_java/wallycore.jar')
    api 'com.fasterxml.jackson.datatype:jackson-datatype-json-org:2.9.6'
    api 'com.android.support:support-v4:27.1.1'
    // force material-dialogs to use latest support libs
    api 'com.android.support:support-v13:27.1.1'
    api 'com.android.support:support-annotations:27.1.1'
    api 'com.android.support:appcompat-v7:27.1.1'
    api 'com.android.support:design:27.1.1'
    api 'com.android.support:recyclerview-v7:27.1.1'
    api 'com.android.support:gridlayout-v7:27.1.1'
    api 'com.google.zxing:core:3.3.2'
    api 'com.google.protobuf:protobuf-java:3.4.0'
    api 'com.android.support:multidex:1.0.3'
    api 'com.squareup.okhttp:okhttp:2.7.5'
    api 'com.github.medyo:android-about-page:1.2.3'
    api 'com.afollestad.material-dialogs:core:0.9.6.0'
    api 'com.android.support:cardview-v7:27.1.1'
    // used to redirect bitcoinj logging to android in debug builds, enable manually
    // debugApi 'com.noveogroup.android:android-logger:1.3.6'
    api 'com.android.support:gridlayout-v7:27.1.1'
    api 'com.journeyapps:zxing-android-embedded:3.5.0'
    api 'com.wefika:flowlayout:0.4.1'
    api 'com.androidadvance:topsnackbar:1.1.1'
}

repositories {
    google()
    jcenter()
}

apply plugin: 'findbugs'

task findbugs(type: FindBugs) {
    ignoreFailures = true
    effort = "default"
    reportLevel = "low"
    excludeFilter = new File("${project.rootDir}/findbugs/findbugs-filter.xml")
    classes = files("${project.rootDir}/app/build/intermediates/classes/production/release/com/greenaddress/")
    source = fileTree('src/main/java/com/greenaddress/')
    classpath = files()
    reports {
        xml.enabled = true
        html.enabled = false
        xml {
            destination "$project.buildDir/findbugs/findbugs-output.xml"
        }
        html {
            destination "$project.buildDir/findbugs/findbugs-output.html"
        }
    }
}
